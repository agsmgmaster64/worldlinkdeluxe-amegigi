const INTRO_STATE_OUTSIDE_TRUCK = 1
const INTRO_STATE_INSIDE_HOME = 3
const INTRO_STATE_CHECK_UPSTAIRS = 4
const INTRO_STATE_SET_CLOCK = 5
const INTRO_STATE_CHECK_DOWNSTAIRS = 6
const INTRO_STATE_FINISED = 7

mapscripts LittlerootTown_BrendansHouse_2F_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		if (var(VAR_LITTLEROOT_INTRO_STATE) == INTRO_STATE_CHECK_UPSTAIRS) {
			setvar(VAR_LITTLEROOT_INTRO_STATE, INTRO_STATE_SET_CLOCK)
		}
		call(SecretBase_EventScript_SetDecorationFlags)
		setvar(VAR_SECRET_BASE_INITIALIZED, 0)
		end
	}
	MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
		VAR_SECRET_BASE_INITIALIZED, 0: SecretBase_EventScript_InitDecorations
	]
}

script LittlerootTown_BrendansHouse_2F_EventScript_WallClock {
	lockall
	if (flag(FLAG_SET_WALL_CLOCK)) {
		goto(PlayersHouse_2F_EventScript_CheckWallClock)
	}
	msgbox(format("The clock is stopped…\p"
		"Better set it and start it!"))
	fadescreen(FADE_TO_BLACK)
	special(StartWallClock)
	waitstate
	delay(30)
	setvar(VAR_LITTLEROOT_INTRO_STATE, INTRO_STATE_CHECK_DOWNSTAIRS)
	setflag(FLAG_SET_WALL_CLOCK)
	setflag(FLAG_HIDE_LITTLEROOT_TOWN_PLAYERS_HOUSE_VIGOROTH_1)
	setflag(FLAG_HIDE_LITTLEROOT_TOWN_PLAYERS_HOUSE_VIGOROTH_2)
	addobject(LOCALID_PLAYERS_HOUSE_2F_MOM)
	applymovement(LOCALID_PLAYERS_HOUSE_2F_MOM, moves(face_right))
	waitmovement(0)
	applymovement(LOCALID_PLAYER, Common_Movement_WalkInPlaceFasterLeft)
	waitmovement(0)
	msgbox(format("{SPEAKER NAME_MOM}{PLAYER}, how do you like your new room?\p"
		"Good! Everything's put away neatly!\p"
		"They finished moving everything in downstairs, too.\p"
		"Puppet movers are so convenient!\p"
		"Oh, you should make sure that everything's all there on your desk."))
	closemessage
	applymovement(LOCALID_PLAYERS_HOUSE_2F_MOM, moves(
		face_left
		delay_8))
	waitmovement(0)
	playse(SE_EXIT)
	removeobject(LOCALID_PLAYERS_HOUSE_2F_MOM)
	releaseall
	end
}

raw `
LittlerootTown_BrendansHouse_2F_EventScript_PC::
	lockall
	setvar VAR_0x8004, PC_LOCATION_BRENDANS_HOUSE
	special DoPCTurnOnEffect
	playse SE_PC_ON
	msgbox gText_PlayerHouseBootPC, MSGBOX_DEFAULT
	special BedroomPC
	waitstate
	releaseall
	end

LittlerootTown_BrendansHouse_2F_EventScript_TurnOffPlayerPC::
	setvar VAR_0x8004, PC_LOCATION_BRENDANS_HOUSE
	playse SE_PC_OFF
	special DoPCTurnOffEffect
	releaseall
	end

PlayersHouse_2F_EventScript_Notebook::
	msgbox PlayersHouse_2F_Text_Notebook, MSGBOX_SIGN
	end

PlayersHouse_2F_EventScript_GameCube::
	msgbox PlayersHouse_2F_Text_ItsAGameCube, MSGBOX_SIGN
	end

PlayersHouse_2F_Text_ClockIsStopped:
	.string "The clock is stopped…\p"
	.string "Better set it and start it!$"

PlayersHouse_2F_Text_HowDoYouLikeYourRoom:
	.string "{PLAYER}, how do you like your\n"
	.string "new room?\p"
	.string "Good! Everything's put away neatly!\p"
	.string "They finished moving everything in\n"
	.string "downstairs, too.\p"
	.string "Puppet movers are so convenient!\p"
	.string "Oh, you should make sure that\n"
	.string "everything's all there on your desk.$"

PlayersHouse_2F_Text_Notebook:
	.string "{PLAYER} flipped open the notebook.\p"
	.string "ADVENTURE RULE NO. 1\n"
	.string "Open the MENU with START.\p"
	.string "ADVENTURE RULE NO. 2\n"
	.string "Record your progress with SAVE.\p"
	.string "The remaining pages are blank…$"

Common_Text_LookCloserAtMap:
	.string "{PLAYER} took a closer look at the\n"
	.string "HOENN region map.$"

PlayersHouse_2F_Text_ItsAGameCube:
	.string "It's a Nintendo GameCube.\p"
	.string "A Game Boy Advance is connected to\n"
	.string "serve as the Controller.$"
`
