const INTRO_STATE_OUTSIDE_TRUCK = 1
const INTRO_STATE_INSIDE_HOME = 3
const INTRO_STATE_CHECK_UPSTAIRS = 4
const INTRO_STATE_SET_CLOCK = 5
const INTRO_STATE_CHECK_DOWNSTAIRS = 6
const INTRO_STATE_FINISED = 7

const LITTLEROOT_STATE_NO_STARTER = 0
const LITTLEROOT_STATE_GOT_STARTER = 1
const LITTLEROOT_STATE_GOT_DEX = 3
const LITTLEROOT_STATE_GOT_MISC_STUFF = 4

mapscripts LittlerootTown_BrendansHouse_1F_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		if (var(VAR_LITTLEROOT_INTRO_STATE) == INTRO_STATE_INSIDE_HOME) {
			setobjectxyperm(LOCALID_PLAYERS_HOUSE_1F_MOM, 9, 8)
			setobjectmovementtype(LOCALID_PLAYERS_HOUSE_1F_MOM, MOVEMENT_TYPE_FACE_UP)
		}
		if (var(VAR_LITTLEROOT_INTRO_STATE) == INTRO_STATE_SET_CLOCK) {
			setobjectxyperm(LOCALID_PLAYERS_HOUSE_1F_MOM, 8, 4)
			setobjectmovementtype(LOCALID_PLAYERS_HOUSE_1F_MOM, MOVEMENT_TYPE_FACE_UP)
		}
		if (var(VAR_LITTLEROOT_INTRO_STATE) == INTRO_STATE_CHECK_DOWNSTAIRS) {
			setobjectxyperm(LOCALID_PLAYERS_HOUSE_1F_MOM, 4, 5)
			setobjectmovementtype(LOCALID_PLAYERS_HOUSE_1F_MOM, MOVEMENT_TYPE_FACE_UP)
		}
		end
	}
	MAP_SCRIPT_ON_LOAD {
		if (var(VAR_LITTLEROOT_INTRO_STATE) < INTRO_STATE_CHECK_DOWNSTAIRS) {
			setmetatile(5, 4, METATILE_BrendansMaysHouse_MovingBox_Open, TRUE)
			setmetatile(5, 2, METATILE_BrendansMaysHouse_MovingBox_Closed, TRUE)
		}
		if (flag(FLAG_RECEIVED_RUNNING_SHOES)) {
			setmetatile(3, 7, METATILE_BrendansMaysHouse_BookOnTable, TRUE)
		}
		end
	}
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_LITTLEROOT_INTRO_STATE, INTRO_STATE_INSIDE_HOME {
			lockall
			setspeakername(gText_Mom)
			msgbox(format("{SPEAKER NAME_MOM}See, {PLAYER}?\N"
				"Isn't it nice in here, too?$"))
			applymovement(LOCALID_PLAYERS_HOUSE_1F_MOM, Common_Movement_FacePlayer)
			waitmovement(0)
			applymovement(LOCALID_PLAYER, Common_Movement_WalkInPlaceFasterRight)
			waitmovement(0)
			msgbox(format("{SPEAKER NAME_MOM}The mover's Puppets do all the work of moving us in and cleaning up after.\N"
				"This is so convenient!\p"
				"{PLAYER}, your room is upstairs.\N"
				"Go check it out, dear!\p"
				"Dad bought you a new clock to mark our move here.\N"
				"Don't forget to set it!$"))
			closemessage
			setvar(VAR_LITTLEROOT_INTRO_STATE, INTRO_STATE_CHECK_UPSTAIRS)
			applymovement(LOCALID_PLAYER, LittlerootTown_BrendansHouse_1F_Movement_WalkUp)
			applymovement(LOCALID_PLAYERS_HOUSE_1F_MOM, Common_Movement_WalkInPlaceFasterUp)
			waitmovement(0)
			releaseall
			end
		}
		VAR_LITTLEROOT_INTRO_STATE, INTRO_STATE_SET_CLOCK {
			lockall
			msgbox("{SPEAKER NAME_MOM}{PLAYER}.\p"
				"Go set the clock in your room, honey.$")
			closemessage
			applymovement(LOCALID_PLAYER, LittlerootTown_BrendansHouse_1F_Movement_WalkUp)
			applymovement(LOCALID_PLAYERS_HOUSE_1F_MOM, LittlerootTown_BrendansHouse_1F_Movement_WalkUp)
			waitmovement(0)
			warp(MAP_LITTLEROOT_TOWN_BRENDANS_HOUSE_2F, 7, 1)
			waitstate
			releaseall
			end
		}
		VAR_LITTLEROOT_INTRO_STATE, INTRO_STATE_CHECK_DOWNSTAIRS {
			lockall
			applymovement(LOCALID_PLAYERS_HOUSE_1F_MOM, Common_Movement_WalkInPlaceFasterRight)
			waitmovement(0)
			playse(SE_PIN)
			applymovement(LOCALID_PLAYERS_HOUSE_1F_MOM, Common_Movement_ExclamationMark)
			waitmovement(0)
			applymovement(LOCALID_PLAYERS_HOUSE_1F_MOM, Common_Movement_Delay48)
			waitmovement(0)
			msgbox(format("{SPEAKER NAME_MOM}Oh! {PLAYER}, {PLAYER}!\N"
				"Quick! Come quickly!"))
			closemessage
			applymovement(LOCALID_PLAYER, moves(
				walk_down * 3
				walk_left * 3
			))
			waitmovement(0)
			playbgm(MUS_ENCOUNTER_INTERVIEWER, FALSE)
			msgbox(format("{SPEAKER NAME_MOM}Look! It's Petalburg Gym!\N"
				"Maybe Dad will be on!"))
			closemessage
			applymovement(LOCALID_PLAYERS_HOUSE_1F_MOM, moves(
				walk_left
				walk_in_place_faster_right
			))
			waitmovement(0)
			applymovement(LOCALID_PLAYER, moves(
				walk_left
				walk_in_place_faster_up))
			waitmovement(0)
			setspeakername(PlayersHouse_1F_Text_Interviewer)
			msgbox(format("…We brought you this report from in front of Petalburg Gym."))
			fadedefaultbgm
			special(TurnOffTVScreen)
			setflag(FLAG_SYS_TV_HOME)
			delay(35)
			applymovement(LOCALID_PLAYER, Common_Movement_WalkInPlaceFasterLeft)
			waitmovement(0)
			msgbox(format("{SPEAKER NAME_MOM}Oh… It's over.\p"
				"I think Dad was on, but we missed him.\N"
				"Too bad.\p"
				"Oh, yes.\N"
				"One of Dad's friends lives in town.\p"
				"PROF. BIRCH is his name.\p"
				"He lives right next door, so you should go over and introduce yourself."))
			closemessage
			setvar(VAR_TEMP_1, 1)
			applymovement(LOCALID_PLAYERS_HOUSE_1F_MOM, moves(
				walk_left
				walk_down
				walk_in_place_faster_right))
			waitmovement(0)
			setvar(VAR_LITTLEROOT_INTRO_STATE, INTRO_STATE_FINISED)
			releaseall
			end
		}
		VAR_LITTLEROOT_HOUSES_STATE_BRENDAN, 1 {
			lockall
			applymovement(LOCALID_PLAYER, moves(
				delay_16
				walk_down * 3
				walk_in_place_faster_left))
			waitmovement(0)
			applymovement(LOCALID_PLAYERS_HOUSE_1F_DAD, Common_Movement_FacePlayer)
			waitmovement(0)
			playse(SE_PIN)
			applymovement(LOCALID_PLAYERS_HOUSE_1F_DAD, Common_Movement_ExclamationMark)
			waitmovement(0)
			applymovement(LOCALID_PLAYERS_HOUSE_1F_DAD, Common_Movement_Delay48)
			waitmovement(0)
			delay(20)
			applymovement(LOCALID_PLAYERS_HOUSE_1F_DAD, moves(walk_right * 2))
			waitmovement(0)
			msgbox(PlayersHouse_1F_Text_TicketFromBrineyCameForYou)
			giveitem(ITEM_SS_TICKET)
			msgbox(PlayersHouse_1F_Text_PortsInSlateportLilycove)
			closemessage
			delay(20)
			applymovement(LOCALID_PLAYERS_HOUSE_1F_MOM, moves(
				walk_up
				walk_right * 4
				walk_down
				walk_in_place_faster_right))
			waitmovement(0)
			applymovement(LOCALID_PLAYERS_HOUSE_1F_DAD, moves(face_left))
			waitmovement(0)
			msgbox(PlayersHouse_1F_Text_BetterGetBackToGym)
			closemessage
			applymovement(LOCALID_PLAYER, moves(
				delay_8
				delay_16
				delay_16
				walk_in_place_faster_down))
			applymovement(LOCALID_PLAYERS_HOUSE_1F_MOM, moves(
				delay_8
				delay_16
				delay_16
				walk_in_place_faster_down))
			applymovement(LOCALID_PLAYERS_HOUSE_1F_DAD, moves(
				walk_down
				walk_right
				walk_down
				delay_8))
			waitmovement(0)
			playse(SE_DOOR)
			removeobject(LOCALID_PLAYERS_HOUSE_1F_DAD)
			setflag(FLAG_RECEIVED_SS_TICKET)
			delay(30)
			applymovement(LOCALID_PLAYERS_HOUSE_1F_MOM, moves(walk_right))
			waitmovement(0)
			applymovement(LOCALID_PLAYER, Common_Movement_WalkInPlaceFasterLeft)
			waitmovement(0)
			delay(20)
			msgbox(PlayersHouse_1F_Text_DadShouldStayLonger)
			closemessage
			setflag(FLAG_SYS_TV_LATIAS_LATIOS)
			special(TurnOnTVScreen)
			delay(60)
			applymovement(LOCALID_PLAYERS_HOUSE_1F_MOM, moves(
				walk_in_place_faster_left
				delay_16 * 4
				walk_in_place_faster_right
				delay_16 * 2))
			waitmovement(0)
			msgbox(PlayersHouse_1F_Text_IsThatABreakingStory)
			closemessage
			applymovement(LOCALID_PLAYER, moves(
				walk_up
				walk_left * 4
				walk_in_place_faster_up))
			waitmovement(0)
			applymovement(LOCALID_PLAYERS_HOUSE_1F_MOM, Common_Movement_WalkInPlaceFasterLeft)
			waitmovement(0)
			msgbox(PlayersHouse_1F_Text_LatiEmergencyNewsFlash)
			closemessage
			clearflag(FLAG_SYS_TV_LATIAS_LATIOS)
			setflag(FLAG_LATIOS_OR_LATIAS_ROAMING)
			special(TurnOffTVScreen)
			applymovement(LOCALID_PLAYERS_HOUSE_1F_MOM, moves(
				walk_up
				walk_left * 2))
			waitmovement(0)
			applymovement(LOCALID_PLAYER, Common_Movement_WalkInPlaceFasterRight)
			waitmovement(0)
			msgbox(PlayersHouse_1F_Text_WhatColorDidTheySay)
			multichoice(22, 8, MULTI_TV_LATI, TRUE)
			copyvar(VAR_0x8004, VAR_RESULT)
			special(InitRoamer)
			copyvar(VAR_ROAMER_POKEMON, VAR_RESULT)
			msgbox(PlayersHouse_1F_Text_StillUnknownPokemon)
			closemessage
			setvar(VAR_LITTLEROOT_HOUSES_STATE_BRENDAN, 2)
			releaseall
			end

		}
	]
}

script LittlerootTown_BrendansHouse_1F_EventScript_GoSeeRoom {
	lockall
	applymovement (LOCALID_PLAYERS_HOUSE_1F_MOM, Common_Movement_WalkInPlaceFasterLeft)
	waitmovement(0)
	msgbox(format("{SPEAKER NAME_MOM}Well, {PLAYER}?\p"
		"Aren't you interested in seeing your very own room?$"))
	closemessage
	applymovement(LOCALID_PLAYERS_HOUSE_1F_MOM, Common_Movement_WalkInPlaceFasterUp)
	applymovement(LOCALID_PLAYER, LittlerootTown_BrendansHouse_1F_Movement_WalkUp)
	waitmovement(0)
	releaseall
	end
}

script LittlerootTown_BrendansHouse_1F_EventScript_Mom {
	lock
	faceplayer
	if (var(VAR_LITTLEROOT_HOUSES_STATE_BRENDAN) == 2) {
		msgbox(PlayersHouse_1F_Text_DontPushYourselfTooHard)
		release
		end
	}
	if (flag(FLAG_HAS_MATCH_CALL)) {
		if (flag(FLAG_ENABLE_MOM_MATCH_CALL)) {
			if (flag(FLAG_BADGE05_GET) && !flag(FLAG_RECEIVED_AMULET_COIN)){
				msgbox(PlayersHouse_1F_Text_GotDadsBadgeHeresSomethingFromMom)
				giveitem(ITEM_DONATION_BOX)
				if (!var(VAR_RESULT)) {
					goto(Common_EventScript_ShowBagIsFull)
				}
				setflag(FLAG_RECEIVED_AMULET_COIN)
				msgbox(PlayersHouse_1F_Text_DontPushYourselfTooHard)
				release
				end
			}
			goto(PlayersHouse_1F_EventScript_MomHealsParty)
		}
		msgbox(PlayersHouse_1F_Text_IsThatAPokenav)
		closemessage
		delay(30)
		playfanfare(MUS_REGISTER_MATCH_CALL)
		msgbox(format("Registered Mom\N"
			"in the iDollNav.$"))
		waitfanfare
		closemessage
		delay(30)
		setflag(FLAG_ENABLE_MOM_MATCH_CALL)
		release
		end
	}
	if (var(VAR_LITTLEROOT_TOWN_STATE) == LITTLEROOT_STATE_GOT_STARTER) {
		goto(PlayersHouse_1F_EventScript_MomHealsParty)
	}
	if (var(VAR_TEMP_1) == 1) {
		msgbox("{SPEAKER NAME_MOM}See you, honey!")
		release
		end
	}
	if (var(VAR_LITTLEROOT_INTRO_STATE) == INTRO_STATE_FINISED) {
		msgbox(format("{SPEAKER NAME_MOM}Did you introduce yourself to PROF. BIRCH?"))
		release
		end
	}
	msgbox(format("{SPEAKER NAME_MOM}See, {PLAYER}?\N"
		"Isn't it nice in here, too?"))
	release
	end
}

script PlayersHouse_1F_EventScript_MomHealsParty {
	msgbox((format"{SPEAKER NAME_MOM}How are you doing, {PLAYER}?\N"
		"You look a little tired.\p"
		"I think you should rest a bit."))
	closemessage
	call(Common_EventScript_OutOfCenterPartyHeal)
	incrementgamestat(GAME_STAT_RESTED_AT_HOME)
	msgbox(format("{SPEAKER NAME_MOM}Take care, honey!"))
	release
	end
}

movement LittlerootTown_BrendansHouse_1F_Movement_WalkUp {
	walk_up
}

raw `
PlayersHouse_1F_Text_GotDadsBadgeHeresSomethingFromMom:
	.string "MOM: Oh? Did DAD give you that BADGE?\p"
	.string "Then here's something from your MOM!$"

PlayersHouse_1F_Text_DontPushYourselfTooHard:
	.string "Don't push yourself too hard, dear.\n"
	.string "You can always come home.\p"
	.string "Go for it, honey!$"

PlayersHouse_1F_Text_IsThatAPokenav:
	.string "MOM: What is that, honey? A POKéNAV?\n"
	.string "Someone from DEVON gave it to you?\p"
	.string "Well, honey, how about registering\n"
	.string "your mom?\p"
	.string "… … …$"

PlayersHouse_1F_Text_Vigoroth1:
	.string "Fugiiiiih!$"

PlayersHouse_1F_Text_Vigoroth2:
	.string "Huggoh, uggo uggo…$"

PlayersHouse_1F_Text_Interviewer:
	.string "Interviewer$"

PlayersHouse_1F_Text_TheresAMovieOnTV:
	.string "There is a movie on TV.\p"
	.string "Two men are dancing on a big piano\n"
	.string "keyboard.\p"
	.string "Better get going!$"

PlayersHouse_1F_Text_RunningShoesManual:
	.string "It's the instruction booklet for the\n"
	.string "RUNNING SHOES.\p"
	.string "“Press the B Button to run while\n"
	.string "wearing your RUNNING SHOES.\p"
	.string "“Lace up your RUNNING SHOES and hit\n"
	.string "the road running!”$"

PlayersHouse_1F_Text_TicketFromBrineyCameForYou:
	.string "DAD: Hm?\p"
	.string "Hey, it's {PLAYER}!\p"
	.string "It's been a while since I saw you,\n"
	.string "but you look…stronger, somehow.\p"
	.string "That's the impression I get.\n"
	.string "But your old man hasn't given up yet!\p"
	.string "Oh, yes, I have something for you.\n"
	.string "This came to you from someone named\l"
	.string "MR. BRINEY.$"

PlayersHouse_1F_Text_PortsInSlateportLilycove:
	.string "DAD: Hm, a TICKET for a ferry?\p"
	.string "If I recall, there are ferry ports in\n"
	.string "SLATEPORT and LILYCOVE.$"

PlayersHouse_1F_Text_BetterGetBackToGym:
	.string "I'd better get back to PETALBURG GYM.\p"
	.string "MOM, thanks for looking after the house\n"
	.string "while I'm away.$"

PlayersHouse_1F_Text_DadShouldStayLonger:
	.string "MOM: That DAD of yours…\p"
	.string "He comes home for the first time in a\n"
	.string "while, but all he talks about is POKéMON.\p"
	.string "He should relax and stay a little longer.$"

PlayersHouse_1F_Text_IsThatABreakingStory:
	.string "MOM: Is that a breaking news story?$"

PlayersHouse_1F_Text_LatiEmergencyNewsFlash:
	.string "We bring you this emergency\n"
	.string "news flash!\p"
	.string "In various HOENN locales, there have\n"
	.string "been reports of a BZZT…colored\l"
	.string "POKéMON in flight.\p"
	.string "The identity of this POKéMON is\n"
	.string "currently unknown.\p"
	.string "We now return you to the regular\n"
	.string "movie program.$"

PlayersHouse_1F_Text_WhatColorDidTheySay:
	.string "MOM: {PLAYER}, did you catch that?\p"
	.string "What color did the announcer say\n"
	.string "that POKéMON was?$"

PlayersHouse_1F_Text_StillUnknownPokemon:
	.string "MOM: Well, isn't that something!\n"
	.string "There are still unknown POKéMON.$"
`
