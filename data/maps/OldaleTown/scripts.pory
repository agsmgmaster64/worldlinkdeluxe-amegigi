const AKYUU_103_STATE_LOOKING = 0
const AKYUU_103_STATE_RETURN = 1
const AKYUU_103_STATE_BACK = 2

mapscripts OldaleTown_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		setflag(FLAG_VISITED_OLDALE_TOWN)
		if (!flag(FLAG_ADVENTURE_STARTED)) {
			setobjectxyperm(LOCALID_FOOTPRINTS_MAN, 1, 11)
			setobjectmovementtype(LOCALID_FOOTPRINTS_MAN, MOVEMENT_TYPE_FACE_LEFT)
		}
		if (!flag(FLAG_RECEIVED_POTION_OLDALE)) {
			setobjectxyperm(LOCALID_OLDALE_MART_EMPLOYEE, 12, 14)
			setobjectmovementtype(LOCALID_OLDALE_MART_EMPLOYEE, MOVEMENT_TYPE_FACE_DOWN)
		}
		end
	}
}

script OldaleTown_EventScript_TownSign {
	msgbox(format("Oldale Town\N"
		"“Where things start off scarce.”"), MSGBOX_SIGN)
	end
}

script OldaleTown_EventScript_Girl {
	msgbox(format("I want to take a rest, so I'm saving my progress."), MSGBOX_NPC)
	end
}

script OldaleTown_EventScript_ShowRivalMessage {
	msgbox(format("WIP Akyuu returns to Littleroot"))
	closemessage
	if (var(VAR_0x8009) == 0 && var(VAR_FACING) != DIR_SOUTH) {
		applymovement(LOCALID_PLAYER, OldaleTown_Movement_WatchRivalExit)
	}
	if (var(VAR_0x8009) == 1) {
		applymovement(LOCALID_PLAYER, OldaleTown_Movement_WatchRivalExit)
	}
	applymovement(LOCALID_OLDALE_RIVAL, OldaleTown_Movement_RivalExit)
	waitmovement(0)
	removeobject(LOCALID_OLDALE_RIVAL)
	setvar(VAR_OLDALE_RIVAL_STATE, AKYUU_103_STATE_BACK)
	setflag(FLAG_HIDE_OLDALE_TOWN_RIVAL)
	releaseall
	end
}

script OldaleTown_EventScript_MartEmployee {
	lock
	faceplayer
	if (flag(FLAG_RECEIVED_POTION_OLDALE) || flag(FLAG_TEMP_1)) {
		msgbox(OldaleTown_Text_PotionExplanation)
		release
		end
	}
	setflag(FLAG_TEMP_1)
	playbgm(MUS_ZGS_FOLLOW_ME, FALSE)
	msgbox(OldaleTown_Text_IWorkAtPokemonMart)
	closemessage
	switch(var(VAR_FACING)) {
		case DIR_SOUTH:
			applymovement(LOCALID_OLDALE_MART_EMPLOYEE, OldaleTown_Movement_EmployeeSouth)
			applymovement(LOCALID_PLAYER, OldaleTown_Movement_PlayerSouth)
			waitmovement(0)
		case DIR_NORTH:
			applymovement(LOCALID_OLDALE_MART_EMPLOYEE, OldaleTown_Movement_EmployeeNorth)
			applymovement(LOCALID_PLAYER, OldaleTown_Movement_PlayerNorth)
			waitmovement(0)
		case DIR_EAST:
			applymovement(LOCALID_PLAYER, OldaleTown_Movement_PlayerEast)
			applymovement(LOCALID_OLDALE_MART_EMPLOYEE, OldaleTown_Movement_EmployeeEast)
			waitmovement(0)
	}
	msgbox(OldaleTown_Text_ThisIsAPokemonMart)
	giveitem(ITEM_POTION)
	if (!var(VAR_RESULT)) {
		msgbox(gText_TooBadBagIsFull)
		fadedefaultbgm
		release
		end
	}
	msgbox(OldaleTown_Text_PotionExplanation)
	setflag(FLAG_RECEIVED_POTION_OLDALE)
	fadedefaultbgm
	release
	end
}

script OldaleTown_EventScript_FootprintsMan {
	lock
	faceplayer
	if (flag(FLAG_ADVENTURE_STARTED)) {
		msgbox(format("I finished sketching the footprints of a rare Puppet.\p"
			"But it turns out they were only my own footprints…"))
		release
		end
	}
	msgbox(format("I just discovered the footprints of a rare Puppet!\p"
		"Wait until I finish sketching them, okay?"))
	closemessage
	applymovement(LOCALID_FOOTPRINTS_MAN, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
	end
}

script OldaleTown_EventScript_BlockedPath {
	lockall
	applymovement(LOCALID_PLAYER, OldaleTown_Movement_PlayerStepBack)
	applymovement(LOCALID_FOOTPRINTS_MAN, OldaleTown_Movement_BackUp)
	waitmovement(0)
	msgbox(format("Aaaaah! Wait!\N"
		"Please don't come in here.\p"
		"I just discovered the footprints of a rare Puppet!\p"
		"Wait until I finish sketching them, okay?"))
	closemessage
	applymovement(LOCALID_FOOTPRINTS_MAN, OldaleTown_Movement_ReturnToOriginalPosition)
	waitmovement(0)
	releaseall
	end
}

raw `
OldaleTown_Movement_EmployeeEast:
	walk_up
	walk_up
	walk_up
	walk_up
	walk_up
	walk_up
	walk_up
	walk_in_place_faster_down
	step_end

OldaleTown_Movement_EmployeeSouth:
	walk_left
	walk_up
	walk_up
	walk_right
	walk_up
	walk_up
	walk_up
	walk_up
	walk_up
	walk_in_place_faster_down
	step_end

OldaleTown_Movement_EmployeeNorth:
	walk_up
	walk_up
	walk_up
	walk_up
	walk_up
	walk_up
	walk_up
	walk_in_place_faster_down
	step_end

OldaleTown_Movement_PlayerEast:
	walk_right
	walk_up
	walk_up
	walk_up
	walk_up
	walk_up
	walk_up
	step_end

OldaleTown_Movement_PlayerSouth:
	delay_16
	delay_16
	delay_16
	delay_16
	walk_up
	walk_up
	walk_up
	walk_up
	walk_up
	step_end

OldaleTown_Movement_PlayerNorth:
	walk_up
	walk_up
	walk_up
	walk_up
	walk_up
	walk_up
	walk_up
	step_end

OldaleTown_EventScript_Rival::
	lockall
	applymovement LOCALID_OLDALE_RIVAL, Common_Movement_FacePlayer
	waitmovement 0
	setvar VAR_0x8009, 0
	goto OldaleTown_EventScript_ShowRivalMessage
	end

OldaleTown_EventScript_RivalTrigger1::
	lockall
	applymovement LOCALID_OLDALE_RIVAL, OldaleTown_Movement_RivalApproachPlayer1
	waitmovement 0
	applymovement LOCALID_PLAYER, Common_Movement_WalkInPlaceFasterRight
	waitmovement 0
	setvar VAR_0x8009, 1
	goto OldaleTown_EventScript_ShowRivalMessage
	end

OldaleTown_EventScript_RivalTrigger2::
	lockall
	applymovement LOCALID_OLDALE_RIVAL, OldaleTown_Movement_RivalApproachPlayer2
	waitmovement 0
	applymovement LOCALID_PLAYER, Common_Movement_WalkInPlaceFasterRight
	waitmovement 0
	setvar VAR_0x8009, 1
	goto OldaleTown_EventScript_ShowRivalMessage
	end

OldaleTown_EventScript_RivalTrigger3::
	lockall
	applymovement LOCALID_OLDALE_RIVAL, OldaleTown_Movement_RivalApproachPlayer3
	waitmovement 0
	applymovement LOCALID_PLAYER, Common_Movement_WalkInPlaceFasterRight
	waitmovement 0
	setvar VAR_0x8009, 1
	goto OldaleTown_EventScript_ShowRivalMessage
	end

OldaleTown_Movement_RivalApproachPlayer1:
	walk_left
	walk_left
	step_end

OldaleTown_Movement_RivalApproachPlayer2:
	walk_left
	step_end

OldaleTown_Movement_RivalApproachPlayer3:
	face_left
	step_end

OldaleTown_Movement_RivalExit:
	walk_down
	walk_down
	walk_down
	walk_down
	walk_down
	walk_down
	step_end

OldaleTown_Movement_WatchRivalExit:
	delay_8
	delay_4
	walk_in_place_faster_down
	step_end

OldaleTown_Movement_PlayerStepBack:
	delay_8
	walk_right
	step_end

OldaleTown_Movement_BackUp:
	walk_fast_up
	walk_in_place_faster_left
	lock_facing_direction
	walk_right
	unlock_facing_direction
	step_end

OldaleTown_Movement_ReturnToOriginalPosition:
	walk_down
	walk_left
	step_end

OldaleTown_Text_IWorkAtPokemonMart:
	.string "Hi!\n"
	.string "I work at a Puppet Mart.\p"
	.string "Can I get you to come with me?$"

OldaleTown_Text_ThisIsAPokemonMart:
	.string "This is a Puppet Mart.\n"
	.string "Just look for our blue roof.\p"
	.string "We sell a variety of goods including\n"
	.string "Toho Orbs for catching Puppets.\p"
	.string "Here, I'd like you to have this as\n"
	.string "a promotional item.$"

OldaleTown_Text_PotionExplanation:
	.string "A Potion can be used anytime, so it's\n"
	.string "even more useful than a Puppet Center\l"
	.string "in certain situations.$"

`
